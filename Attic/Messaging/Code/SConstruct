from os import environ, path, pathsep


def EmbedManifest(env, target):
    (EXE_TYPE, DLL_TYPE) = (1, 2)
    
    type = str(DLL_TYPE if str(target).endswith('.dll') else EXE_TYPE)
    manifest = '-manifest ${TARGET}.manifest'
    output = '-outputresource:$TARGET;' + type
    
    env.AddPostAction(target, 'mt -nologo %s %s' % (manifest, output))
    env.Clean(target, '%s.manifest' % target)


def Tools(env):
    tools = env['TOOLS']
    
    names = {
        'GNU': ['gcc', 'g++', 'mingw'],
        'MSVC': ['msvc'],
    }
    
    for (name, aliases) in names.items():
        for alias in aliases:
            if alias in tools:
                return name


np_api_sdk = path.join(environ['NP_API_SDK'], 'include')
libpurple_include_paths = environ['LIBPURPLE_INCLUDE_PATHS'].split(pathsep)

env = Environment(
    CPPPATH = [np_api_sdk] + libpurple_include_paths,
    #LIBS = ['purple'],
)

tools = Tools(env)
name = 'npMessaging'
files = Glob('*.cpp')

if tools == 'GNU':
    env.Append(CCFLAGS = ['-ansi', '-pedantic', '-Wall'])
elif tools == 'MSVC':
    files += env.RES(name + '.rc')
    env['WINDOWS_INSERT_DEF'] = True
    env.Append(CCFLAGS = ['/EHsc', '/MD'], CPPDEFINES = ['_WINDOWS', '_X86_'])

lib = env.SharedLibrary(name, files)[0]

if tools == 'MSVC':
    EmbedManifest(env, lib)
