from os import environ, path, pathsep


def EmbedManifest(env, target):
    (EXE_TYPE, DLL_TYPE) = (1, 2)
    
    type = str(DLL_TYPE if str(target).endswith('.dll') else EXE_TYPE)
    manifest = '-manifest ${TARGET}.manifest'
    output = '-outputresource:$TARGET;' + type
    
    env.AddPostAction(target, 'mt -nologo %s %s' % (manifest, output))
    env.Clean(target, '%s.manifest' % target)


def Tools(env):
    tools = env['TOOLS']
    
    names = {
        'GNU': ['gcc', 'g++', 'mingw'],
        'MSVC': ['msvc'],
    }
    
    for (name, aliases) in names.items():
        for alias in aliases:
            if alias in tools:
                return name


np_api_sdk = path.join(environ['NP_API_SDK'], 'include')
libpurple_include_paths = environ['LIBPURPLE_INCLUDE_PATHS'].split(pathsep)

env = Environment(
    CPPDEFINES = ['WIN32', '_DEBUG', '_WINDOWS', 'XP_WIN32', 'MOZILLA_STRICT_API', 'XPCOM_GLUE', 'XP_WIN', '_X86_', 'NPMESSAGING_EXPORTS', '_CRT_SECURE_NO_WARNINGS', '_CRT_SECURE_NO_DEPRECATE', '_WINDLL', '_UNICODE', 'UNICODE'],
    CPPPATH = [np_api_sdk] + libpurple_include_paths,
    #LIBS = ['purple'],
    WINDOWS_INSERT_DEF = True,
)

tools = Tools(env)
name = 'npMessaging'
files = Glob('*.cpp')

if tools == 'GNU':
    env.Append(CCFLAGS = ['-ansi', '-pedantic', '-Wall'])
elif tools == 'MSVC':
    files += env.RES(name + '.rc')
    env.Append(CCFLAGS = ['/EHsc', '/MD'])

lib = env.SharedLibrary(name, files)[0]

if tools == 'MSVC':
    EmbedManifest(env, lib)
