import os


def EmbedManifest(env, target):
    (EXE_TYPE, DLL_TYPE) = (1, 2)
    
    file_type = EXE_TYPE if str(target).endswith('.exe') else DLL_TYPE
    manifest = '-manifest ${TARGET}.manifest'
    output = '-outputresource:$TARGET;' + str(file_type)
    
    env.AddPostAction(target, 'mt -nologo %s %s' % (manifest, output))
    env.Clean(target, '%s.manifest' % target)


def Tools(env):
    tools = env['TOOLS']
    
    names = {
        'GNU': ['gcc', 'g++', 'mingw'],
        'MSVC': ['msvc'],
    }
    
    for (name, aliases) in names.items():
        for alias in aliases:
            if alias in tools:
                return name


env = Environment(
    CPPDEFINES = ['MOZILLA_STRICT_API'],
    CPPPATH = os.environ['NP_API_SDK_INCLUDE_PATH'].split(os.pathsep),
)

tools = Tools(env)
name = 'npExample'
files = Glob('*.cpp')

if tools == 'GNU':
    env.Append(CCFLAGS = ['-ansi', '-Wall'])
elif tools == 'MSVC':
    files += env.RES(name + '.rc')
    env['WINDOWS_INSERT_DEF'] = True
    env.Append(CPPDEFINES = ['_WINDOWS', '_X86_'], CCFLAGS = ['/EHsc', '/MD'])

lib = env.SharedLibrary(name, files)[0]

if tools == 'MSVC':
    EmbedManifest(env, lib)
